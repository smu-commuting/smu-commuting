plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.flywaydb.flyway" version "7.0.3"
	id "org.asciidoctor.convert" version "1.5.9.2"
	id 'java'
}

group = 'com.api'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	/**
	 * Security
	 */
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.junit.jupiter:junit-jupiter:5.7.0'

	/**
	 * Lombok
	 */
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	/**
	 * JWT Dependencies
	 */
	implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.1', 'io.jsonwebtoken:jjwt-jackson:0.11.1'

	/**
	 * Mail
	 */
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	/**
	 * Database
	 */
	runtimeOnly 'mysql:mysql-connector-java'
	implementation "org.flywaydb:flyway-core"

	/**
	 * Test Dependencies
	 */
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	/**
	 * RestDocs
	 */
	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.4.RELEASE'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

jar {
	enabled = false
}

tasks.named('test') {
	useJUnitPlatform()
}

/*************************
 * RestDocs
 *************************/
ext {
	set('snippetsDir', file("build/generated-snippets"))
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/asciidoc/html5/")
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

/*************************
 * Flyway
 *************************/
flyway {
	baselineVersion = 0
	encoding = 'UTF-8'
	validateOnMigrate = true
	baselineOnMigrate = true
	locations = ["filesystem:${file('src/main/resources/db/migration').absolutePath}"]
}